(ns meteobot.app.command
  (:require
   [clojure.string :as str]
   [taoensso.telemere :refer [log!]]
   [mlib.telegram.botapi :as botapi]
   [meteobot.data.store :as store]
   [meteobot.app.fmt :as fmt] 
   [meteobot.app.subs :as subs]
   ,))


(def ^:const ACTIVE_HOURS 3)

(def ^:const ACTIVE_PAGE_SIZE 5)
(def ^:const NEAR_PAGE_SIZE 3)

(def DEFAULT_LOCATION 
  {:latitude 52.27 :longitude 104.27})


(def START_TEXT
  (str
   "–í—ã –ø–æ–¥–∫–ª—é—á–∏–ª–∏—Å—å –∫ @meteo38bot.\n"
   "–î–ª—è –ø–æ–ª—É—á–µ–Ω–∏—è –∫—Ä–∞—Ç–∫–æ–π –∏–Ω—Å—Ç—Ä—É–∫—Ü–∏–∏ –∏—Å–ø–æ–ª—å–∑—É–π—Ç–µ –∫–æ–º–∞–Ω–¥—É /help."
   ))


(def HELP_TEXT
  (str
   "üî∏ –≠—Ç–æ—Ç –±–æ—Ç –ø—Ä–µ–¥–æ—Å—Ç–∞–≤–ª—è–µ—Ç –∏–Ω—Ñ–æ—Ä–º–∞—Ü–∏—é —Å —Å–µ—Ç–∏ –∞–≤—Ç–æ–º–∞—Ç–∏—á–µ—Å–∫–∏—Ö –º–µ—Ç–µ–æ—Å—Ç–∞–Ω—Ü–∏–π"
   " <a href=\"https://meteo38.ru\">meteo38.ru</a>"
   " –∏ –≤–æ–∑–º–æ–∂–Ω–æ—Å—Ç—å –Ω–∞—Å—Ç—Ä–æ–∏—Ç—å —Ä–∞—Å—Å—ã–ª–∫—É —É–≤–µ–¥–æ–º–ª–µ–Ω–∏–π –≤ —É–∫–∞–∑–∞–Ω–Ω–æ–µ –≤—Ä–µ–º—è –ø–æ –¥–∞–Ω–Ω—ã–º —Å–∞–π—Ç–∞.\n\n"
   "üî∏ –ü–æ –∫–Ω–æ–ø–∫–µ '<b>‚≠ê –ò–∑–±—Ä–∞–Ω–Ω–æ–µ</b>' –≤—ã–≤–æ–¥—è—Ç—Å—è –ø–æ—Å–ª–µ–¥–Ω–∏–µ –¥–∞–Ω–Ω—ã–µ —Å –≤—ã–±—Ä–∞–Ω–Ω—ã—Ö –º–µ—Ç–µ–æ—Å—Ç–∞–Ω—Ü–∏–π.\n\n"
   "üî∏ –ö–Ω–æ–ø–∫–∞ '<b>üåç –†—è–¥–æ–º</b>' –∏—Å–ø–æ–ª—å–∑—É–µ—Ç —Ñ—É–Ω–∫—Ü–∏—é –≥–µ–æ–ª–æ–∫–∞—Ü–∏–∏ –¥–ª—è –ø–æ–∏—Å–∫–∞ –±–ª–∏–∂–∞–π—à–∏—Ö —Å—Ç–∞–Ω—Ü–∏–π.\n\n"
   "üî∏ –ò–∑ '<b>–ú–µ–Ω—é</b>' –º–æ–∂–Ω–æ –≤–æ—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å—Å—è –æ—Å—Ç–∞–ª—å–Ω—ã–º–∏ –∫–æ–º–∞–Ω–¥–∞–º–∏ –∏ —É–ø—Ä–∞–≤–ª—è—Ç—å —Ä–∞—Å—Å—ã–ª–∫–∞–º–∏.\n\n"
   "üì© –ü–æ–∂–µ–ª–∞–Ω–∏—è –ø–æ —Ä–∞–±–æ—Ç–µ –±–æ—Ç–∞ –ø–∏—à–∏—Ç–µ –≤ –≥—Ä—É–ø–ø—É - @meteo38\n"
   ,))


(def EMPTY_FAVS 
  (str "üî∏ –ß—Ç–æ–±—ã –¥–æ–±–∞–≤–∏—Ç—å —Å—Ç–∞–Ω—Ü–∏—é –≤ –∏–∑–±—Ä–∞–Ω–Ω–æ–µ\n\n"
       "- –∑–∞–π–¥–∏—Ç–µ –≤ —Å–ø–∏—Å–æ–∫ —Å—Ç–∞–Ω—Ü–∏–π /active –∏–ª–∏ /near\n"
       "- –ø–µ—Ä–µ–π–¥–∏—Ç–µ –Ω–∞ –∏–Ω—Ñ–æ—Ä–º–∞—Ü–∏—é –æ —Å—Ç–∞–Ω—Ü–∏–∏ –ø–æ —Å—Å—ã–ª–∫–µ '‚ÑπÔ∏è info_'\n"
       "- –Ω–∞–∂–º–∏—Ç–µ –Ω–∞ –∫–Ω–æ–ø–∫—É ‚ûï."))


(defn parse-command [s]
  (when s
    (when-let [[_ cmd param] (re-matches #"^[/!]([A-Za-z0-9]+)[ _]*(.*)$" s)]
      [cmd (if (str/blank? param) nil param)]
      )))


(comment
  
  (parse-command "/start test_123 456")
  ;;=> ["start" "test_123 456"]
  
  (parse-command "")
  ;;=> nil
  
  (parse-command "qwe 123")
  ;;=> nil
  
  (parse-command "/start ")
  ;;=> ["start" nil]
  
  (parse-command "!hello 1 2 3")
  ;;=> ["hello" "1 2 3"]
  
  ,)


(defn stinfo [cfg {{chat-id :id} :chat} {st :param}]
  (if-let [st-data (store/station-info st)]
    (let [favs (set (store/user-favs chat-id))
          msg (fmt/st-info st-data 
                           {:show-info-link false 
                            :reply-markup (fmt/kbd-fav-subs st (favs st))})
          ]
      (botapi/send-message cfg chat-id msg))
    (botapi/send-html cfg chat-id 
                      (str "–°—Ç–∞–Ω—Ü–∏—è –Ω–µ –Ω–∞–π–¥–µ–Ω–∞!\n‚ö†Ô∏è <b>" st "</b>"))))


(defn cmd-map [cfg {chat :chat} {st :param}]
  (if-let [stinfo (store/station-info st)]
    (botapi/send-location cfg (:id chat) 
                   {:latitude (:lat stinfo) :longitude (:lon stinfo)
                    :reply_markup {:inline_keyboard
                                   [[{:text (str "üìà " (:title stinfo)) 
                                      :url (fmt/meteo-st-link st)}]]}})
    (botapi/send-html cfg (:id chat) (str "–°—Ç–∞–Ω—Ü–∏—è –Ω–µ –Ω–∞–π–¥–µ–Ω–∞!\n‚ö†Ô∏è <b>" st "</b>"))))


; - - - - - - - - - -

(defn cmd-start [cfg {chat :chat :as msg} {st :param}]
  (botapi/send-message cfg (:id chat)
                {:text START_TEXT
                 :parse_mode "HTML"
                 :reply_markup fmt/main-buttons})
  (when st
    (stinfo cfg msg {:param st})))


(defn cmd-help [cfg {{chat-id :id} :chat} _]
  (botapi/send-message cfg chat-id
                       {:text HELP_TEXT
                        :parse_mode "HTML"
                        :reply_markup fmt/main-buttons
                        }))


; - - - - - - - - - -

(defn set-fav-flag [st-list favs]
  (map #(if (favs (:st %)) (assoc % :is-fav true) %) st-list))


(defn active-stations-for-location [location]
  (when location
    (store/active-stations (:latitude location) (:longitude location) ACTIVE_HOURS)))


(defn next-keyboard [prefix offset]
  {:inline_keyboard [[{:text "–ï—â—ë..." :callback_data (str prefix "_next:" offset)}]]})


(defn prev-next-keyboard [prefix prev-page next-page]
  (let [btns [(when prev-page
                {:text "‚¨ÖÔ∏è" :callback_data (str prefix ":" prev-page)})
              (when next-page
                {:text "‚û°Ô∏è" :callback_data (str prefix ":" next-page)})]]
    {:inline_keyboard [(remove nil? btns)]}))


(defn next-messages [st-list format-fn kbd-more]
  (if kbd-more
    (let [btn-idx (dec (count st-list))]
      (->> st-list
           (map-indexed 
            (fn [i st]
              (cond-> (format-fn st)
                (= i btn-idx) (assoc :reply_markup kbd-more)
                ,)))))
    (map format-fn st-list)))


(defn on-location [cfg {{chat-id :id} :chat} {:keys [latitude longitude] :as location}]
  (log! ["location:" {:chat-id chat-id :location location}])
  (store/set-user-location chat-id location)
  (botapi/send-html cfg chat-id
             (str "üìå " (fmt/float6 latitude) " " (fmt/float6 longitude)))
  (let [st-list (active-stations-for-location location)
        [page rest] (split-at NEAR_PAGE_SIZE st-list)
        more-kbd (when (seq rest) (next-keyboard "near" NEAR_PAGE_SIZE))
        favs (set (store/user-favs chat-id))
        ]
    (doseq [msg (next-messages (set-fav-flag page favs) 
                               #(fmt/st-info % nil) 
                               more-kbd)]
      (botapi/send-message cfg chat-id msg))
    ,))


(defn cmd-near [cfg {{chat-id :id} :chat :as msg} _]
  (let [location (or (store/get-user-location chat-id) 
                     DEFAULT_LOCATION)]
    (on-location cfg msg location)))


(defn cb-near-next [cfg 
                    {{{chat-id :id} :chat msg-id :message_id} :message :as cbk} 
                    [_ offset]]
  (if-let [n (parse-long offset)]
    (let [location (store/get-user-location chat-id)
          acts (drop n (active-stations-for-location location))
          page (take NEAR_PAGE_SIZE acts)
          rest (drop NEAR_PAGE_SIZE acts)
          more-kbd (when (seq rest) (next-keyboard "near" (+ n NEAR_PAGE_SIZE)))
          favs (set (store/user-favs chat-id))
          ]
      (when-not location
        (log! ["cb-next-near: user location missing"]))
      
      (botapi/edit-reply-markup cfg chat-id msg-id {})
      
      (doseq [msg (next-messages (set-fav-flag page favs) 
                                 #(fmt/st-info % nil) 
                                 more-kbd)]
        (botapi/send-message cfg chat-id msg))
      ,)
    ;;
    (log! :warn ["cb-near-next: invalid offset" cbk])
    ))


; - - - - - - - - - -

(defn active-list-message [st-list kbd-more]
  {:text (str/join "\n" (map fmt/active-list-item st-list))
   :parse_mode "HTML"
   :link_preview_options {:is_disabled true}
   :reply_markup (or kbd-more fmt/main-buttons)
   ,})


(defn cmd-active [cfg {{chat-id :id} :chat} _]
  (let [location (or (store/get-user-location chat-id) 
                     DEFAULT_LOCATION)
        [page rest] (split-at ACTIVE_PAGE_SIZE (active-stations-for-location location))
        kbd (when (seq rest) (prev-next-keyboard "active" nil 1))
        favs (set (store/user-favs chat-id))
        ]
    (->> 
     (active-list-message (set-fav-flag page favs) kbd)
     (botapi/send-message cfg chat-id)
     ,)))


(defn cb-active [cfg
                 {{{chat-id :id} :chat msg-id :message_id} :message}
                 [_ page-num]]
  (let [pgn (or (parse-long page-num) 0)
        offset (* ACTIVE_PAGE_SIZE pgn)
        location (or (store/get-user-location chat-id)
                     DEFAULT_LOCATION)
        st-list (active-stations-for-location location)
        [page rest] (->> st-list (drop offset) (split-at ACTIVE_PAGE_SIZE))
        kbd (prev-next-keyboard "active"
                                (when (> pgn 0) (dec pgn))
                                (when (seq rest) (inc pgn)))
        favs (set (store/user-favs chat-id))]
    (->>
     (active-list-message (set-fav-flag page favs) kbd)
     (botapi/edit-message cfg chat-id msg-id))
    ,))


; - - - - - - - - - -

(defn cmd-favs [cfg {{chat-id :id} :chat :as msg} opts]
  (if-let [fvs (seq (store/user-favs chat-id))]
    (doseq [st fvs]
      (stinfo cfg msg (assoc opts :param st)))
    (botapi/send-html cfg chat-id EMPTY_FAVS)))


(defn cb-fav [cfg 
              {{{chat-id :id} :chat msg-id :message_id} :message cbk-id :id :as cbk}
              [_ add-del st]
              ]
  (case add-del
    "add" (when-let [err-msg (:error (store/user-fav-add chat-id st))]
            (botapi/answer-callback-text cfg cbk-id err-msg))
    "del" (store/user-fav-del chat-id st)
    (log! :warn ["cb-fav: wrong add-del parameter" cbk]))
  (let [favs (set (store/user-favs chat-id))]
    (botapi/edit-reply-markup cfg chat-id msg-id (fmt/kbd-fav-subs st (favs st)))
    ))


(defn favs-brief [cfg {{chat-id :id} :chat}]
  (if-let [st-list (seq (store/user-favs chat-id))]
    (doseq [st st-list]
      (when-let [st-data (store/station-info st)]
        (botapi/send-message cfg chat-id (fmt/st-brief st-data)))
      ,)
    (botapi/send-html cfg chat-id EMPTY_FAVS)
    ))


; - - - - - - - - - -

(def command-map
  {"start"  cmd-start
   "help"   cmd-help
   "info"   stinfo
   "near"   cmd-near
   "map"    cmd-map
   "favs"   cmd-favs
   "sub"    subs/cmd-subs-edit
   "subs"   subs/cmd-subs
   "active" cmd-active
   ,})


(defn route-command [cfg msg text]
  (when-let [[cmd param] (parse-command text)]
    (when-let [hc (get command-map cmd)]
      (hc cfg msg {:param param})
      true
      )))


(defn route-text [cfg msg text]
  (condp = text
    fmt/BTN_FAVS_TEXT 
    (do
      (favs-brief cfg msg)
      true)
    nil))


(defn route-location [cfg msg location]
  (when location
    (on-location cfg msg location)
    true
    ))


(def callback-map
  {
   "near_next" cb-near-next
   "active"    cb-active
   "fav"       cb-fav
   "subs_new"  subs/cb-subs-new
   "subs"      subs/cb-subs
   ,})


(defn route-callback [cfg {data :data :as cbk}]
  (let [params (str/split data #":")]
    (when-let [h-cbk (get callback-map (first params))]
      (h-cbk cfg cbk params)
      true)
    ))


(def MENU_COMMANDS 
  [
   {:command "active" :description "–ê–∫—Ç–∏–≤–Ω—ã–µ —Å—Ç–∞–Ω—Ü–∏–∏"}
   {:command "favs"   :description "–ò–∑–±—Ä–∞–Ω–Ω–æ–µ"}
   {:command "subs"   :description "–†–∞—Å—Å—ã–ª–∫–∏"}
   {:command "near"   :description "–†—è–¥–æ–º"}
   {:command "help"   :description "–ö—Ä–∞—Ç–∫–∞—è —Å–ø—Ä–∞–≤–∫–∞"}
   ])


(defn setup-menu-commands [cfg]
  (botapi/set-my-commands cfg MENU_COMMANDS nil nil))
